
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформлениеДляОтделов();
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеДляОтделов()
	
	Настройки = Отчет.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦветаОтделов.Отдел,
	|	ЦветаОтделов.Цвет
	|ИЗ
	|	РегистрСведений.ЦветаОтделов КАК ЦветаОтделов";
	Выборка = Запрос.Выполнить().Выбрать();
	
	//Удаляем спец найстройки которые уже были
	Номер = 0;
	Количество = Настройки.Элементы.Количество();
	Пока Номер < Количество Цикл
		Настройка = Настройки.Элементы[Номер];
		Если Найти(Настройка.Представление, "СпецНайстройка_") Тогда
			Настройки.Элементы.Удалить(Настройка);
		Иначе 
			Номер = Номер + 1;
		КонецЕсли;
	КонецЦикла;
	
	//Устанавливаем новые настройки
	Номер = 0;
	Пока Выборка.Следующий() Цикл
		
		Попытка
			Цвет = WebЦвета[Выборка.Цвет];
		Исключение
			Продолжить;
		КонецПопытки;
		
		НоваяНастройка = Настройки.Элементы.Добавить();
		
		НоваяНастройка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
		НоваяНастройка.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
		НоваяНастройка.Использование = Истина;
		НоваяНастройка.Представление = "СпецНайстройка_"+Номер;
		НоваяНастройка.ПредставлениеПользовательскойНастройки = "СпецНайстройка_"+Номер;
		
		НоваяНастройка.Отбор.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор); 
		Отбор = НоваяНастройка.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.Использование = Истина;
		Отбор.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
		Отбор.Представление = "ОтборОтдела";
		Отбор.ПредставлениеПользовательскойНастройки = "ОтборОтдела";
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбращениеОт");
		Отбор.ПравоеЗначение = Выборка.Отдел;
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		
		Оформление = НоваяНастройка.Оформление.Элементы.Найти("ЦветФона");
		Оформление.Использование = Истина;
		Оформление.Значение = Цвет;
		Оформление.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
		Оформление.ПредставлениеПользовательскойНастройки = "ОформлениеОтдела";
		
		Поля = НоваяНастройка.Поля.Элементы;
		Для Каждого ДоступныеПоля Из НоваяНастройка.Поля.ДоступныеПоляОформляемыхПолей.Элементы Цикл
			Если НЕ ДоступныеПоля.Папка Тогда
				НовоеПоле = Поля.Добавить();
				НовоеПоле.Использование = Истина;
				НовоеПоле.Поле = ДоступныеПоля.Поле;
			КонецЕсли;
		КонецЦикла;
		
		Номер = Номер + 1;
	КонецЦикла;
	
КонецПроцедуры
