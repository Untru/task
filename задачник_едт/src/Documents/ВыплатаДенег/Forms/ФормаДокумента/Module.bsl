

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ВидРасчета = Перечисления.ВидыРасчета.СИсполнителями;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНераспределеннуюСумму()
	НераспределеннаяСумма = Объект.Сумма - Объект.РасшифровкаПлатежа.Итог("Сумма");
	Элементы.НераспределеннаяСумма.Видимость = (НераспределеннаяСумма <> 0);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСуммаПриИзменении(Элемент)
	ОбновитьНераспределеннуюСумму();
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	ОбновитьНераспределеннуюСумму();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗадачи(Команда)
	Если Объект.РасшифровкаПлатежа.Количество() Тогда
		ОписаниеЗавершения = Новый ОписаниеОповещения("ЗаполнитьРасшифровкуФрагмент", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеЗавершения, "Строки будут удалены. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьРасшифровкуНаСервере();
		ОбновитьНераспределеннуюСумму();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасшифровкуФрагмент(Ответ, Параметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	ЗаполнитьРасшифровкуНаСервере();
	ОбновитьНераспределеннуюСумму();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасшифровкуНаСервере()
	
	Объект.РасшифровкаПлатежа.Очистить();
	
	Запрос = Новый Запрос;
	Если Объект.ВидРасчета <> Перечисления.ВидыРасчета.СИсполнителями Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапросаПоИсполнителям();
	Запрос.УстановитьПараметр("Исполнитель", Объект.Исполнитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() И Объект.Сумма > Объект.РасшифровкаПлатежа.Итог("Сумма") Цикл
		НоваяСтрокаРасшифровки = Объект.РасшифровкаПлатежа.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРасшифровки, Выборка);
		НоваяСтрокаРасшифровки.Сумма = Мин(Объект.Сумма - Объект.РасшифровкаПлатежа.Итог("Сумма"), Выборка.СуммаВыборки)
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаПоИсполнителям()
	Возврат "ВЫБРАТЬ
	|	ВзаиморасчетыСИсполнителямиОстатки.Задача,
	|	ВзаиморасчетыСИсполнителямиОстатки.СуммаКОплатеОстаток КАК СуммаВыборки
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСИсполнителями.Остатки КАК ВзаиморасчетыСИсполнителямиОстатки
	|ГДЕ
	|	ВзаиморасчетыСИсполнителямиОстатки.Исполнитель = &Исполнитель";
КонецФункции



