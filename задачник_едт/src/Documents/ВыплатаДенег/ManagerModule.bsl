#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаВзаиморасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаВзаиморасчетыСПартнерами(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаВзаиморасчетыСИсполнителями(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.ВидРасчета КАК ВидРасчета,
	|	ДанныеДокумента.СпособПлатежа КАК СпособПлатежа,
	|	ДанныеДокумента.Исполнитель КАК Исполнитель,
	|	ДанныеДокумента.Сумма КАК СуммаДокумента,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.Номер,
	|	ВложенныйЗапрос.Сумма КАК СуммаПоСтрокам
	|ИЗ
	|	Документ.ВыплатаДенег КАК ДанныеДокумента,
	|	(ВЫБРАТЬ
	|		СУММА(ВыплатаДенегРасшифровкаПлатежа.Сумма) КАК Сумма
	|	ИЗ
	|		Документ.ВыплатаДенег.РасшифровкаПлатежа КАК ВыплатаДенегРасшифровкаПлатежа
	|	ГДЕ
	|		ВыплатаДенегРасшифровкаПлатежа.Ссылка = &Ссылка) КАК ВложенныйЗапрос
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",            Реквизиты.Период);
	Запрос.УстановитьПараметр("ВидРасчета",        Реквизиты.ВидРасчета);
	Запрос.УстановитьПараметр("СпособПлатежа",     Реквизиты.СпособПлатежа);
	Запрос.УстановитьПараметр("Исполнитель",       Реквизиты.Исполнитель);
	Запрос.УстановитьПараметр("СуммаДокумента",    Реквизиты.СуммаДокумента);
	Запрос.УстановитьПараметр("СуммаПоСтрокам",    Реквизиты.СуммаПоСтрокам);
	Запрос.УстановитьПараметр("Номер",             Реквизиты.Номер);
	Запрос.УстановитьПараметр("ПометкаУдаления",   Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("Проведен",          Реквизиты.Проведен);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВзаиморасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ВзаиморасчетыСКлиентами";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	Значение(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ВыплатаДенегРасшифровкаПлатежа.Этап КАК Этап,
	|	-ВыплатаДенегРасшифровкаПлатежа.Сумма КАК Сумма,
	|	-ВыплатаДенегРасшифровкаПлатежа.Сумма КАК СуммаКОплате,
	|	ВыплатаДенегРасшифровкаПлатежа.Сумма КАК СуммаОплаченная
	|ИЗ
	|	Документ.ВыплатаДенег.РасшифровкаПлатежа КАК ВыплатаДенегРасшифровкаПлатежа
	|ГДЕ
	|	ВыплатаДенегРасшифровкаПлатежа.Ссылка = &Ссылка
	|	И &ВидРасчета = ЗНАЧЕНИЕ(Перечисление.ВидыРасчета.СКлиентом)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	Значение(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Документ.ЭтапПроекта.ПустаяСсылка) КАК Этап,
	|	-(&СуммаДокумента - &СуммаПоСтрокам) КАК Сумма,
	|	-(&СуммаДокумента - &СуммаПоСтрокам) КАК СуммаКОплате,
	|	&СуммаДокумента - &СуммаПоСтрокам КАК СуммаОплаченная
	|ИЗ
	|	Документ.ВыплатаДенег КАК ВыплатаДенег
	|ГДЕ
	|	ВыплатаДенег.Ссылка = &Ссылка
	|	И &ВидРасчета = ЗНАЧЕНИЕ(Перечисление.ВидыРасчета.СКлиентом)
	|	И &СуммаДокумента - &СуммаПоСтрокам > 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВзаиморасчетыСПартнерами(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ВзаиморасчетыСПартнерами";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ДанныеТЧДокумента.Этап КАК Этап,
	|	-ДанныеТЧДокумента.Сумма КАК Сумма,
	|	-ДанныеТЧДокумента.Сумма КАК СуммаКОплате,
	|	ДанныеТЧДокумента.Сумма КАК СуммаОплаченная
	|ИЗ
	|	Документ.ВыплатаДенег.РасшифровкаПлатежа КАК ДанныеТЧДокумента
	|ГДЕ
	|	ДанныеТЧДокумента.Ссылка = &Ссылка
	|	И &ВидРасчета = ЗНАЧЕНИЕ(Перечисление.ВидыРасчета.СПартнером)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Документ.ЭтапПроекта.ПустаяСсылка) КАК Этап,
	|	-(&СуммаДокумента - &СуммаПоСтрокам) КАК Сумма,
	|	-(&СуммаДокумента - &СуммаПоСтрокам) КАК СуммаКОплате,
	|	&СуммаДокумента - &СуммаПоСтрокам КАК СуммаОплаченная
	|ИЗ
	|	Документ.ВыплатаДенег КАК ВыплатаДенег
	|ГДЕ
	|	ВыплатаДенег.Ссылка = &Ссылка
	|	И &ВидРасчета = ЗНАЧЕНИЕ(Перечисление.ВидыРасчета.СПартнером)
	|	И &СуммаДокумента - &СуммаПоСтрокам > 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВзаиморасчетыСИсполнителями(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ВзаиморасчетыСИсполнителями";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Исполнитель КАК Исполнитель,
	|	ДанныеТЧДокумента.Задача КАК Задача,
	|	-ДанныеТЧДокумента.Сумма КАК Сумма,
	|	-ДанныеТЧДокумента.Сумма КАК СуммаКОплате,
	|	ДанныеТЧДокумента.Сумма КАК СуммаОплаченная
	|ИЗ
	|	Документ.ВыплатаДенег.РасшифровкаПлатежа КАК ДанныеТЧДокумента
	|ГДЕ
	|	ДанныеТЧДокумента.Ссылка = &Ссылка
	|	И &ВидРасчета = ЗНАЧЕНИЕ(Перечисление.ВидыРасчета.СИсполнителями)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК Исполнитель,
	|	ЗНАЧЕНИЕ(Документ.ОбращениеВПоддержку.ПустаяСсылка) КАК Задача,
	|	-(&СуммаДокумента - &СуммаПоСтрокам) КАК Сумма,
	|	-(&СуммаДокумента - &СуммаПоСтрокам) КАК СуммаКОплате,
	|	&СуммаДокумента - &СуммаПоСтрокам КАК СуммаОплаченная
	|ИЗ
	|	Документ.ВыплатаДенег КАК ВыплатаДенег
	|ГДЕ
	|	ВыплатаДенег.Ссылка = &Ссылка
	|	И &ВидРасчета = ЗНАЧЕНИЕ(Перечисление.ВидыРасчета.СИсполнителями)
	|	И &СуммаДокумента - &СуммаПоСтрокам > 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли