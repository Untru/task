&НаСервере
Процедура УстановитьОтборСпискаЭтапов()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокЭтапов.Отбор, "Проект", Объект.Ссылка); 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписокаЗадач(ВыбранныйЭтап)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокЗадачЭтапа.Отбор, "ЭтапПроекта", ВыбранныйЭтап);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЭтаповПриАктивизацииСтроки(Элемент)
	ВыбранныйЭтап = Неопределено;
	Если ЗначениеЗаполнено(Объект.Ссылка) И Элемент.ТекущиеДанные <> Неопределено Тогда
		ВыбранныйЭтап = Элемент.ТекущиеДанные.Ссылка;
	КонецЕсли;
	УстановитьОтборСписокаЗадач(ВыбранныйЭтап);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	УстановитьОтборСпискаЭтапов();
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьОтборСпискаЭтапов();
	УстановитьСумму();
КонецПроцедуры

&НаКлиенте
Процедура СписокЭтаповПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Перед добавлением строки документ необходимо записать.'")); 
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗадачЭтапаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Перед добавлением строки документ необходимо записать.'")); 
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСумму()
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СебестоимостьОбороты.ПлановаяСтоимостьОборот
		|ИЗ
		|	РегистрНакопления.Себестоимость.Обороты КАК СебестоимостьОбороты
		|ГДЕ
		|	СебестоимостьОбороты.Проект = &Проект";
	
	Запрос.УстановитьПараметр("Проект", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Объект.Сумма = ВыборкаДетальныеЗаписи.ПлановаяСтоимостьОборот;
	Иначе
		Объект.Сумма = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанЭтапПроекта" И Объект.Ссылка = Параметр Тогда 
		УстановитьСумму();
	КонецЕсли;
	
КонецПроцедуры




