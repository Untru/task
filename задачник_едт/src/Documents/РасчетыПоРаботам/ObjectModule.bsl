
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	УдалитьДвижения();
	
	
	СтруктураДвижений = Новый Структура;
	ПолучитьТаблицыДвижений(СтруктураДвижений);
	Для Каждого ТДвижений из СтруктураДвижений Цикл
		Движения[ТДвижений.Ключ].Загрузить(ТДвижений.Значение);
		Движения[ТДвижений.Ключ].Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьДвижения()
	Для Каждого ТаблицаДвижений из Движения Цикл
		ТаблицаДвижений.Очистить();
		ТаблицаДвижений.Записать();
	КонецЦикла;
КонецПроцедуры

Процедура ПолучитьТаблицыДвижений(СтруктураДвижений)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыПоРаботамРасшифровка.Ссылка КАК Регистратор,
	|	РасчетыПоРаботамРасшифровка.Задача.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	РасчетыПоРаботамРасшифровка.Задача,
	|	РасчетыПоРаботамРасшифровка.Задача.ОбращениеОт КАК Отделение,
	|	РасчетыПоРаботамРасшифровка.Задача.РеальныйИсполнитель КАК Исполнитель,
	|	РасчетыПоРаботамРасшифровка.Сумма
	|ИЗ
	|	Документ.РасчетыПоРаботам.Расшифровка КАК РасчетыПоРаботамРасшифровка
	|ГДЕ
	|	РасчетыПоРаботамРасшифровка.Ссылка = &Ссылка
	|	И РасчетыПоРаботамРасшифровка.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасчетов.ОплатаИсполнителю)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоРаботамРасшифровка.Ссылка КАК Регистратор,
	|	РасчетыПоРаботамРасшифровка.Ссылка.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	РасчетыПоРаботамРасшифровка.Задача,
	|	РасчетыПоРаботамРасшифровка.Задача.ОбращениеОт КАК Отделение,
	|	РасчетыПоРаботамРасшифровка.Сумма
	|ИЗ
	|	Документ.РасчетыПоРаботам.Расшифровка КАК РасчетыПоРаботамРасшифровка
	|ГДЕ
	|	РасчетыПоРаботамРасшифровка.Ссылка = &Ссылка
	|	И РасчетыПоРаботамРасшифровка.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасчетов.ОплатаЗаказчиком)";
	Результат = Запрос.ВыполнитьПакет();
	
	СтруктураДвижений.Вставить("РасчетСИсполнителями", Результат[0].Выгрузить());
	СтруктураДвижений.Вставить("ЗадолженостьКлиентов", Результат[1].Выгрузить());
КонецПроцедуры




