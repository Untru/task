#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаСебестоимость(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаВзаиморасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаВзаиморасчетыСПартнерами(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Проект КАК Проект,
	|	ДанныеДокумента.НаименованиеЭтапа КАК НаименованиеЭтапа,
	|	ДанныеДокумента.ПлановаяСтоимость КАК ПлановаяСтоимость,
	|	ДанныеДокумента.Завершен КАК Завершен,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.Номер
	|ИЗ
	|	Документ.ЭтапПроекта КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",            Реквизиты.Период);
	Запрос.УстановитьПараметр("Проект",            Реквизиты.Проект);
	Запрос.УстановитьПараметр("НаименованиеЭтапа", Реквизиты.НаименованиеЭтапа);
	Запрос.УстановитьПараметр("ПлановаяСтоимость", Реквизиты.ПлановаяСтоимость);
	Запрос.УстановитьПараметр("Завершен",          Реквизиты.Завершен);
	Запрос.УстановитьПараметр("Номер",             Реквизиты.Номер);
	Запрос.УстановитьПараметр("ПометкаУдаления",   Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("Проведен",          Реквизиты.Проведен);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаСебестоимость(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "Себестоимость";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Ссылка КАК Этап,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка) КАК СтатьяЗатрат,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЗатрат.ПустаяСсылка) КАК ВидЗатрат,
	|	&ПлановаяСтоимость КАК ПлановаяСтоимость,
	|	0 КАК Себестоимость,
	|	0 КАК ДополнительныеЗатраты
	|ИЗ
	|	Документ.ЭтапПроекта КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВзаиморасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ВзаиморасчетыСКлиентами";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ДокументПроект.Контрагент КАК Контрагент,
	|	&Ссылка КАК Этап,
	|	&ПлановаяСтоимость КАК Сумма
	|ИЗ
	|	Документ.ЭтапПроекта КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Проект КАК ДокументПроект
	|		ПО ДанныеДокумента.Проект = ДокументПроект.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВзаиморасчетыСПартнерами(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ВзаиморасчетыСПартнерами";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СУММА(СебестоимостьОбороты.ПлановаяСтоимостьОборот) КАК ПлановаяСтоимость,
	|	СУММА(СебестоимостьОбороты.СебестоимостьОборот + ВЫБОР
	|		КОГДА СебестоимостьОбороты.ВидЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыЗатрат.ОплаченоПавлом)
	|			ТОГДА СебестоимостьОбороты.ДополнительныеЗатратыОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СебестоимостьИДопЗатратыПавел,
	|	СУММА(СебестоимостьОбороты.СебестоимостьОборот + СебестоимостьОбороты.ДополнительныеЗатратыОборот) КАК
	|		СебестоимостьИДопЗатратыВсе
	|ПОМЕСТИТЬ втПервичныеПоказателиЭтапа
	|ИЗ
	|	РегистрНакопления.Себестоимость.Обороты(,,, Этап = &Ссылка) КАК СебестоимостьОбороты";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "");
	
	//|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Ссылка КАК Этап,
	|	втПервичныеПоказателиЭтапа.СебестоимостьИДопЗатратыПавел + (втПервичныеПоказателиЭтапа.ПлановаяСтоимость -
	|		втПервичныеПоказателиЭтапа.ПлановаяСтоимость * СтавкаНалога.Значение / 100 -
	|		втПервичныеПоказателиЭтапа.СебестоимостьИДопЗатратыВсе) / 2 КАК Сумма,
	|	ВЫБОР
	|		КОГДА &Завершен
	|			ТОГДА втПервичныеПоказателиЭтапа.СебестоимостьИДопЗатратыПавел + (втПервичныеПоказателиЭтапа.ПлановаяСтоимость -
	|				втПервичныеПоказателиЭтапа.ПлановаяСтоимость * СтавкаНалога.Значение / 100 -
	|				втПервичныеПоказателиЭтапа.СебестоимостьИДопЗатратыВсе) / 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаКОплате,
	|	0 КАК СуммаОплаченная
	|ИЗ
	|	Документ.ЭтапПроекта КАК ДанныеДокумента,
	|	Константа.СтавкаНалога КАК СтавкаНалога,
	|	втПервичныеПоказателиЭтапа КАК втПервичныеПоказателиЭтапа
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли