//Объект - документ объект ОбращениеВПоддержку 
Процедура ИзменитьДетальноеПланированиеПриИзмененииШапки(Объект, ИмяТаблицы, ПриЗаписи = Ложь, ЗаменятьДатыТаблиц = Истина) Экспорт
	Количество = Объект[ИмяТаблицы].Количество();
	
	Если ЗначениеЗаполнено(Объект.ДатаНачалаПлан) И Объект.ДатаНачалаПлан = Объект.ДатаОкончанияПлан Тогда 
		Объект.ДатаОкончанияПлан = Объект.ДатаОкончанияПлан + 3600;
	КонецЕсли;
	
	Если НЕ Количество Тогда
		Строка = Объект[ИмяТаблицы].Добавить();
		Строка.ДатаНачала = Объект.ДатаНачалаПлан;
		Строка.ДатаОкончания = Объект.ДатаОкончанияПлан;
	ИначеЕсли Количество = 1 И НЕ ЗаменятьДатыТаблиц Тогда
		Если НЕ ЗначениеЗаполнено(Объект[ИмяТаблицы][0].ДатаНачала) 
			ИЛИ Объект[ИмяТаблицы][0].ДатаНачала = Объект.Ссылка.ДатаНачалаПлан Тогда
			Объект[ИмяТаблицы][0].ДатаНачала 					= Объект.ДатаНачалаПлан;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект[ИмяТаблицы][Количество - 1].ДатаОкончания) 
			ИЛИ Объект[ИмяТаблицы][Количество - 1].ДатаОкончания = Объект.Ссылка.ДатаОкончанияПлан Тогда
			Объект[ИмяТаблицы][Количество - 1].ДатаОкончания	= Объект.ДатаОкончанияПлан;
		КонецЕсли;
	ИначеЕсли ЗаменятьДатыТаблиц Тогда
		Объект[ИмяТаблицы][0].ДатаНачала 					= Объект.ДатаНачалаПлан;
		Объект[ИмяТаблицы][Количество - 1].ДатаОкончания	= Объект.ДатаОкончанияПлан;
	КонецЕсли;
	
	Если НЕ (ЗначениеЗаполнено(Объект.ДатаНачалаПлан) И ЗначениеЗаполнено(Объект.ДатаОкончанияПлан)) И ПриЗаписи Тогда
		Объект[ИмяТаблицы].Очистить();
	КонецЕсли;
	
КонецПроцедуры

//Объект - документ объект ОбращениеВПоддержку 
Процедура ИзменитьДетальноеПланированиеПриИзмененииТаблицы(Объект) Экспорт
	Количество = Объект.ДетализированыйПланВыполнения.Количество();
	
	Если НЕ Количество Тогда
		Объект.ДатаНачалаПлан = Дата("00010101");
		Объект.ДатаОкончанияПлан = Дата("00010101");
	Иначе
		ДатаНачалаПлан = Дата("21010101");
		ДатаОкончанияПлан = Дата("00010101");
		
		Для Каждого ТекСтрока Из Объект.ДетализированыйПланВыполнения Цикл
			Если ЗначениеЗаполнено(ТекСтрока.ДатаНачала) И ТекСтрока.ДатаНачала < ДатаНачалаПлан Тогда
				ДатаНачалаПлан = ТекСтрока.ДатаНачала;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекСтрока.ДатаОкончания) И ТекСтрока.ДатаОкончания < ДатаНачалаПлан Тогда
				ДатаНачалаПлан = ТекСтрока.ДатаОкончания;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекСтрока.ДатаНачала) И ТекСтрока.ДатаНачала > ДатаОкончанияПлан Тогда
				ДатаОкончанияПлан = ТекСтрока.ДатаНачала;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекСтрока.ДатаОкончания) И ТекСтрока.ДатаОкончания > ДатаОкончанияПлан Тогда
				ДатаОкончанияПлан = ТекСтрока.ДатаОкончания;
			КонецЕсли;
		КонецЦикла;
		
		Объект.ДатаНачалаПлан	= ?(ЗначениеЗаполнено(ДатаНачалаПлан) И НЕ ДатаНачалаПлан = Дата("21010101"), ДатаНачалаПлан, Объект.ДатаНачалаПлан);
		Объект.ДатаОкончанияПлан= ?(ЗначениеЗаполнено(ДатаОкончанияПлан), ДатаОкончанияПлан, Объект.ДатаОкончанияПлан);
	КонецЕсли;
	
КонецПроцедуры

//Объект - документ объект ОбращениеВПоддержку
Функция ПроверитьКорректностьПериодизации(Объект, ИмяТаблицы, ЭтоОбъектФормы = Ложь, Ошибка = "") Экспорт
	
	Корректно = Истина;
	ПредПериод = Неопределено;
	Для Каждого ТекСтрока Из Объект[ИмяТаблицы] Цикл
		Если НЕ ПредПериод = Неопределено И ТекСтрока.ДатаНачала < ПредПериод 
			ИЛИ ТекСтрока.ДатаНачала >= ТекСтрока.ДатаОкончания Тогда
			Корректно = Ложь;
			
			Если ЭтоОбъектФормы Тогда
				ТекСтрока.НеКорректнаяСтрока = Истина;
			Иначе
				Прервать;
			КонецЕсли;
			
		Иначе 
			ПредПериод = ТекСтрока.ДатаОкончания;
			
			Если ЭтоОбъектФормы Тогда
				ТекСтрока.НеКорректнаяСтрока = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Корректно;
КонецФункции
